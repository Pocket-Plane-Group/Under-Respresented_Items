INCLUDE ~underrep/lib/functions.tpa~ // load cd_items_casting_spells

ACTION_IF ((enhanced_edition) AND (FILE_EXISTS ~underrep/languages/%LANGUAGE%/setup_ee.tra~)) BEGIN

  LOAD_TRA ~underrep/languages/%LANGUAGE%/setup_ee.tra~ // override normal item descriptions with EE-formatted ones
 
END 

COPY ~underrep/bam~     ~override~
     ~underrep/eff~     ~override~
     ~underrep/spl~     ~override~

COPY ~underrep/itm/U#AX2H85.itm~ ~override~
  SAY NAME2 @3 
  SAY DESC @4

COPY ~underrep/itm/U#AX2H86.itm~ ~override~
  SAY NAME2 @5 
  SAY DESC @6

COPY ~underrep/itm/U#AX2H87.itm~ ~override~
  SAY NAME2 @7 
  SAY DESC @8

COPY ~underrep/itm/U#AX2H93.itm~ ~override~
  SAY NAME2 @9 
  SAY DESC @10

COPY ~underrep/itm/U#AX2H94.itm~ ~override~
  SAY NAME2 @11 
  SAY DESC @12

COPY ~underrep/itm/U#AX2H95.itm~ ~override~
  SAY NAME2 @13 
  SAY DESC @14

COPY ~underrep/itm/U#AX2H98.itm~ ~override~
  SAY NAME2 @15 
  SAY DESC @16

COPY ~underrep/itm/U#AX2H99.itm~ ~override~
  SAY NAME2 @17 
  SAY DESC @18 

COPY ~underrep/itm/U#BELT05.itm~ ~override~
  SAY NAME2 @19 
  SAY DESC @20 

COPY ~underrep/itm/U#CHAN07.itm~ ~override~
  SAY NAME2 @21 
  SAY DESC @22 

COPY ~underrep/itm/U#DAGG90.itm~ ~override~
  SAY NAME2 @23 
  SAY DESC @24 

COPY ~underrep/itm/U#DAGG96.itm~ ~override~
  SAY NAME2 @25 
  SAY DESC @26 

COPY ~underrep/itm/U#DAGG98.itm~ ~override~
  SAY NAME2 @27 
  SAY DESC @28 
  
COPY_EXISTING ~dart03.itm~ ~override~ // get stun dart projectile
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      READ_LONG  (abil_off + 0x26 + (index * 0x38)) dart_flags
      READ_SHORT (abil_off + 0x2a + (index * 0x38)) stundart_proj
      SET index = abil_num // kill loop
    END
  END
  BUT_ONLY

COPY ~underrep/itm/U#DART83.itm~ ~override~
  SAY NAME2 @29 
  SAY DESC @30
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_LONG  (abil_off + 0x26 + (index * 0x38)) dart_flags
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) stundart_proj
      SET index = abil_num // kill loop
    END
  END
  
COPY_EXISTING ~dart05.itm~ ~override~ // asp nest projectile
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      READ_SHORT (abil_off + 0x2a + (index * 0x38)) poisondart_proj
      SET index = abil_num // kill loop
    END
  END
  BUT_ONLY

COPY ~underrep/itm/U#DART84.itm~ ~override~
  SAY NAME2 @31 
  SAY DESC @32
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_LONG  (abil_off + 0x26 + (index * 0x38)) dart_flags
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) poisondart_proj
      SET index = abil_num // kill loop
    END
  END

COPY ~underrep/itm/U#HALB95.itm~ ~override~
  SAY NAME2 @33 
  SAY DESC @34

COPY ~underrep/itm/U#HALB96.itm~ ~override~
  SAY NAME2 @35 
  SAY DESC @36

COPY ~underrep/itm/U#HALB97.itm~ ~override~
  SAY NAME2 @37 
  SAY DESC @38

COPY ~underrep/itm/U#HAMM94.itm~ ~override~
  SAY NAME2 @39 
  SAY DESC @40
  
COPY_EXISTING ~dart04.itm~ ~override~ // dart of wounding projectile
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      READ_SHORT (abil_off + 0x2a + (index * 0x38)) wounddart_proj
      SET index = abil_num // kill loop
    END
  END
  BUT_ONLY

COPY ~underrep/itm/U#HFDTDD.itm~ ~override~
  SAY NAME2 @41 
  SAY DESC @42
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_LONG  (abil_off + 0x26 + (index * 0x38)) dart_flags
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) wounddart_proj
      SET index = abil_num // kill loop
    END
  END

COPY ~underrep/itm/U#HFDTPD.itm~ ~override~
  SAY NAME2 @43 
  SAY DESC @44 
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_LONG  (abil_off + 0x26 + (index * 0x38)) dart_flags
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) poisondart_proj
      SET index = abil_num // kill loop
    END
  END

COPY ~underrep/itm/U#HFHBHL.itm~ ~override~
  SAY NAME2 @45 
  SAY DESC @46

COPY ~underrep/itm/U#HFSRIM.itm~ ~override~
  SAY NAME2 @47 
  SAY DESC @48

COPY ~underrep/itm/U#HFSSLS.itm~ ~override~
  SAY NAME2 @49 
  SAY DESC @50
  PATCH_IF MOD_IS_INSTALLED ~BG2FIXPACK/SETUP-BG2FIXPACK.TP2~ ~114~  BEGIN // add immunity to stun for free acrion if OBC fixpack stuff installed
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 =   210 timing = 2 END // immunity to pw: stun
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 =    45 timing = 2 END // immunity to stun
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 169 target = 1 parameter2 =    55 timing = 2 END // prevent stun icon
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 target = 1 parameter1 =  1280 timing = 2 END // prevent 'stunned' string
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 target = 1 parameter1 = 14043 timing = 2 END // prevent 'stun' string
  END

COPY ~underrep/itm/U#MACE92.itm~ ~override~
  SAY NAME2 @51 
  SAY DESC @52

COPY ~underrep/itm/U#MACE94.itm~ ~override~
  SAY NAME2 @53 
  SAY DESC @54

COPY ~underrep/itm/U#MACE95.itm~ ~override~
  SAY NAME2 @55 
  SAY DESC @56

COPY ~underrep/itm/U#SPER89.itm~ ~override~
  SAY NAME2 @57 
  SAY DESC @58

COPY ~underrep/itm/U#STAF86.itm~ ~override~
  SAY NAME2 @59 
  SAY DESC @60

COPY ~underrep/itm/U#STAF90.itm~ ~override~
  SAY NAME2 @61 
  SAY DESC @62

COPY ~underrep/itm/U#SWDB94.itm~ ~override~
  SAY NAME2 @63 
  SAY DESC @64 

COPY ~underrep/itm/U#SWDB95.itm~ ~override~
  SAY NAME2 @65 
  SAY DESC @66 

COPY ~underrep/itm/U#SWDBRC.itm~ ~override~
  SAY NAME2 @67 
  SAY DESC @68 

// dynamically add effects of chaotic commands as equipping effects
LAF cd_items_casting_spells INT_VAR cast_at_level = 1 item_header = 0 duration = 0 STR_VAR item = ~u#swdbrc~ spell = sppr508 END
COPY_EXISTING ~u#swdbrc.itm~ ~override~ // make these equipping effects, not time-limited
  LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode =  61 END
  LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode = 174 END
  LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode = 215 END
  LPF ALTER_EFFECT INT_VAR check_headers = 0 power = 0 timing = 2 resist_dispel = 0 END
  BUT_ONLY
  
COPY ~underrep/itm/U#SWDS94.itm~ ~override~
  SAY NAME2 @69 
  SAY DESC @70

COPY ~underrep/itm/U#HFHRCR.itm~ ~override~
  SAY NAME2 @71 
  SAY DESC @72

COPY ~underrep/itm/U#HFSRNP.itm~ ~override~
  SAY NAME2 @73 
  SAY DESC @74
  READ_LONG 0x0c name_hfsrnp

COPY ~underrep/itm/U#HFSFWS.itm~ ~override~
  SAY NAME2 @75 
  SAY DESC @76
  READ_LONG 0x0c name_hfsfws

COPY ~underrep/itm/U#CLUBBT.itm~ ~override~
  SAY NAME2 @77 
  SAY DESC @78

COPY ~underrep/itm/U#HFHEAH.itm~ ~override~
  SAY NAME2 @79 
  SAY DESC @80

COPY ~underrep/itm/U#HFHRVA.itm~ ~override~
  SAY NAME2 @81 
  SAY DESC @82

COPY ~underrep/itm/U#HFDGSH.itm~ ~override~
  SAY NAME2 @83 
  SAY DESC @84 

COPY ~underrep/itm/U#HFDGXF.itm~ ~override~
  SAY NAME2 @85 
  SAY DESC @86 

COPY ~underrep/itm/U#CLUBMP.itm~ ~override~
  SAY NAME2 @87 
  SAY DESC @88

COPY ~underrep/itm/U#HAMMBI.itm~ ~override~
  SAY NAME2 @89 
  SAY DESC @90

COPY ~underrep/itm/U#HFHBPB.itm~ ~override~
  SAY NAME2 @91 
  SAY DESC @92

COPY ~underrep/itm/U#ZZL6MA.ITM~ ~override~         
  SAY NAME2 @93 
  SAY DESC @94

COPY_EXISTING ~itemexcl.2da~ ~override~ // add new armor to the protection stuff list  
  REPLACE_TEXTUALLY CASE_INSENSITIVE ~^\(chan13\)\([ %TAB%].+\)$~ ~\1\2
u#chan07\2~
  REPLACE_TEXTUALLY CASE_INSENSITIVE ~^\(leat20\)\([ %TAB%].+\)$~ ~\1\2
u#hfheah\2~
  PRETTY_PRINT_2DA
  BUT_ONLY IF_EXISTS

// add tooltips for multi-ability items 
APPEND ~tooltip.2da~ ~u#hfsfws %name_hfsfws% 32427 -1~    // Caballus' Whispering Staff (mass invis)
APPEND ~tooltip.2da~ ~u#hfsrnp %name_hfsrnp% 25914 16963~ // Twelve Paces (breach & lower resistance)

// create insect plague without traveling projectile
COPY_EXISTING ~insec2.pro~ ~override/u#hfdtpd.pro~
  WRITE_ASCII 0x104 ~~ #8 // remove traveling animation
ADD_PROJECTILE ~override/u#hfdtpd.pro~
COPY_EXISTING ~sppr517.spl~ ~override/u#hfdtpd.spl~
  LPF ALTER_SPELL_HEADER INT_VAR projectile = ~u#hfdtpd~ END  

// make creatures who were immune to orig remove magic also immune to clone
COPY_EXISTING ~spellh01.itm~ ~override~
  LPF CLONE_EFFECT STR_VAR match_resource = spwi302 resource = ~u#hfdtdd~ END     
  BUT_ONLY IF_EXISTS

// make creatures who were immune to orig insect plague also immune to clone
COPY_EXISTING ~ohbetrai.itm~ ~override~
              ~gorchr.itm~   ~override~
              ~ohbdem01.cre~ ~override~
  LPF CLONE_EFFECT STR_VAR match_resource = sppr517 resource = ~u#hfdtpd~ END     
  BUT_ONLY IF_EXISTS

// add generic name/descripts to 2h axes
COPY_EXISTING ~u#ax2h85.itm~ ~override~
              ~u#ax2h86.itm~ ~override~
              ~u#ax2h87.itm~ ~override~
              ~u#ax2h93.itm~ ~override~
              ~u#ax2h94.itm~ ~override~
              ~u#ax2h95.itm~ ~override~
              ~u#ax2h98.itm~ ~override~
              ~u#ax2h99.itm~ ~override~ 
  SAY 0x08 @95
  SAY 0x50 @96
  BUT_ONLY
  
ACTION_IF enhanced_edition BEGIN

  // make 2h axes available to shamans
  COPY_EXISTING ~u#ax2h85.itm~ ~override~
                ~u#ax2h86.itm~ ~override~
                ~u#ax2h87.itm~ ~override~
                ~u#ax2h93.itm~ ~override~
                ~u#ax2h94.itm~ ~override~
                ~u#ax2h95.itm~ ~override~
                ~u#ax2h98.itm~ ~override~
                ~u#ax2h99.itm~ ~override~ 
    WRITE_BYTE 0x21 (THIS BAND `BIT6) // removes druid restriction, add back as 319 below
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = 11 parameter2 = 5 timing = 2 special = 1080 END
    BUT_ONLY
    
  // skip poison effects for immune creatures or poison immunity >= 100
  COPY_EXISTING ~u#ax2h87.itm~ ~override~ // Greataxe of Blurred Visions +5
                ~u#dagg90.itm~ ~override~ // Viper's Fang +2
                ~u#dart84.itm~ ~override~ // Returning Poison Tipped Dart
                ~u#hfsrim.itm~ ~override~ // Life's Blood Drinker
                ~u#hfssls.itm~ ~override~ // Lolth's Cruel Sting
                ~u#staf90.itm~ ~override~ // Staff of Greater Spell Resistance +5
                ~u#swdb94.itm~ ~override~ // Miasmic Bastard Sword
                ~u#swds94.itm~ ~override~ // Charged Short Sword of Wounding +5
    LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 6 END // delete poison icon...
    LPF ALTER_EFFECT INT_VAR match_opcode = 25 special = 6 END            // and attach directly to poison effect
    LPF CLONE_EFFECT INT_VAR match_opcode = 25 opcode = 324 parameter2 = 55 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 special = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END // golems/undead
    LPF CLONE_EFFECT INT_VAR match_opcode = 25 opcode = 324 parameter2 = 77 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 special = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END // poison resist >= 100
    BUT_ONLY

  // use ee's HP draining flags instead of damage/hp gain
  COPY_EXISTING ~u#ax2h99.itm~ ~override~ // Greataxe of Vampiric Flame
                ~u#dagg98.itm~ ~override~ // Vampiric Dagger +1
                ~u#halb96.itm~ ~override~ // Massive Halberd of Hate +4
                ~u#hfsrim.itm~ ~override~ // Life's Blood Drinker
    LPF DELETE_EFFECT INT_VAR match_opcode = 18 END // delete max HP gain
    LPF ALTER_EFFECT  INT_VAR match_opcode = 12 match_parameter1 = 2 duration = 60 special = (BIT3 + BIT6) END // add hp draining flags to vampiric damage
    BUT_ONLY
    
  /////                                                  \\\\\
  ///// EE fixpack stuff                                 \\\\\
  /////                                                  \\\\\
    
  // use immunity spellstates, if available (e.g. EE Fixpack)
  OUTER_SET confusion_immunity = IDS_OF_SYMBOL (~splstate~ ~CONFUSION_IMMUNITY~)  
  OUTER_SET death_immunity     = IDS_OF_SYMBOL (~splstate~ ~DEATH_IMMUNITY~)   
  OUTER_SET entangle_immunity  = IDS_OF_SYMBOL (~splstate~ ~ENTANGLE_IMMUNITY~)  
  OUTER_SET haste_immunity     = IDS_OF_SYMBOL (~splstate~ ~HASTE_IMMUNITY~)  
  OUTER_SET hold_immunity      = IDS_OF_SYMBOL (~splstate~ ~HOLD_IMMUNITY~)  
  OUTER_SET panic_immunity     = IDS_OF_SYMBOL (~splstate~ ~PANIC_IMMUNITY~)  
  OUTER_SET silence_immunity   = IDS_OF_SYMBOL (~splstate~ ~SILENCE_IMMUNITY~)   
  OUTER_SET slow_immunity      = IDS_OF_SYMBOL (~splstate~ ~SLOW_IMMUNITY~)  
  OUTER_SET stun_immunity      = IDS_OF_SYMBOL (~splstate~ ~STUN_IMMUNITY~)  
  OUTER_SET web_immunity       = IDS_OF_SYMBOL (~splstate~ ~WEB_IMMUNITY~)    
  
  ACTION_IF (confusion_immunity > 0) BEGIN 
  
    COPY_EXISTING ~u#clubbc.spl~ ~override~ // Monkey Paw of Discipline, confusion subspell
                  ~u#swdbrc.itm~ ~override~ // Rage of Chaos
      LPF CLONE_EFFECT INT_VAR match_opcode = 128 opcode = 324 parameter1 = confusion_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END
      BUT_ONLY
  
  END   
  
  ACTION_IF (death_immunity > 0) BEGIN 
  
    COPY_EXISTING ~u#zzl6ma.itm~ ~override~ // Mace of Goblin Slaying +1
      LPF CLONE_EFFECT INT_VAR match_opcode = 55 multi_match = 1 opcode = 324 parameter1 = death_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END
      BUT_ONLY
  
  END         
  
  ACTION_IF ((entangle_immunity > 0) OR (haste_immunity > 0) OR (hold_immunity > 0) OR (slow_immunity > 0) OR (web_immunity > 0))BEGIN // free action... le sigh
  
    COPY_EXISTING ~u#hfssls.itm~ ~override~ // Lolth's Cruel Sting
      PATCH_IF (entangle_immunity > 0) BEGIN
        LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 154 opcode = 328 parameter2 = entangle_immunity special = 1 STR_VAR insert = first END // clone immunity to entangle into set spell state
      END  
      PATCH_IF (haste_immunity > 0) BEGIN
        LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 16 opcode = 328 parameter2 = haste_immunity special = 1 STR_VAR insert = first END // clone immunity to haste into set spell state
      END  
      PATCH_IF (hold_immunity > 0) BEGIN
        LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 175 opcode = 328 parameter2 = hold_immunity special = 1 STR_VAR insert = first END // clone immunity to hold into set spell state
      END  
      PATCH_IF (slow_immunity > 0) BEGIN
        LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 40 opcode = 328 parameter2 = slow_immunity special = 1 STR_VAR insert = first END // clone immunity to slow into set spell state
      END  
      PATCH_IF ((stun_immunity > 0) AND (MOD_IS_INSTALLED ~BG2FIXPACK/SETUP-BG2FIXPACK.TP2~ ~114~)) BEGIN // if stun immunity from BG2 Fixpack
        LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 45 opcode = 328 parameter2 = stun_immunity special = 1 STR_VAR insert = first END // clone immunity to stun into set spell state
      END  
      PATCH_IF (web_immunity > 0) BEGIN
        LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 157 opcode = 328 parameter2 = web_immunity special = 1 STR_VAR insert = first END // clone immunity to web into set spell state
        LPF CLONE_EFFECT INT_VAR match_opcode = 157 opcode = 324 parameter1 = web_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 
          STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END // web immune creatures block melee web effect
      END  
      BUT_ONLY 
  
  END 
  
  ACTION_IF (panic_immunity > 0) BEGIN 
  
    COPY_EXISTING ~u#clubbp.spl~ ~override~ // Monkey Paw of Discipline, panic subspell
                  ~u#hfhrva.itm~ ~override~ // The Dire-Hammer Valorfoe
      LPF CLONE_EFFECT INT_VAR match_opcode =  24 opcode = 324 parameter1 = panic_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END
      BUT_ONLY
  
  END  
  
  ACTION_IF (silence_immunity > 0) BEGIN 
  
    COPY_EXISTING ~u#hfsfws.itm~ ~override~ // Caballus' Whispering Staff
      LPF CLONE_EFFECT INT_VAR match_opcode = 38 opcode = 324 parameter1 = silence_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END
      BUT_ONLY
  
  END     
  
  ACTION_IF (stun_immunity > 0) BEGIN 
  
    COPY_EXISTING ~u#dart83.itm~ ~override~ // Returning Stun Dart +1
                  ~u#hfhbpb.itm~ ~override~ // Pudu's Fiery Blight
                  ~u#mace92.itm~ ~override~ // Mace of Stunning Frost Burst
                  ~u#sper89.itm~ ~override~ // Spear of Stunning +2
                  ~u#stun3.spl~  ~override~ // Miasmic Bastard Sword
      LPF CLONE_EFFECT INT_VAR match_opcode = 45 opcode = 324 parameter1 = stun_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 STR_VAR insert = above resource = EVAL ~%SOURCE_RES%~ END
      BUT_ONLY
  
  END     

  COPY_EXISTING ~#curebld.spl~ ~override~ // eefp for curing blindness
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#ax2h85~ END // curing blind should cure delayed sound effect from Holy Greataxe of Justice +5
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#hfdtdd~ END // curing blind should cure delayed sound effect from Delnar's Eye Gouger
    BUT_ONLY IF_EXISTS
    
  COPY_EXISTING ~#curecon.spl~ ~override~ // eefp for curing confusion
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#clubbc~ END // confusion spell from monkey paw of disciple
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#cwdbrc~ END // chaos from rage of chaos
    BUT_ONLY IF_EXISTS
    
  COPY_EXISTING ~#curefer.spl~ ~override~ // eefp for curing fear
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#clubbp~ END // fear spell from monkey paw of disciple
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#hfhrva~ END // fear from The Dire-Hammer Valorfoe
    BUT_ONLY IF_EXISTS
    
  COPY_EXISTING ~#curesil.spl~ ~override~ // eefp for curing silence
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 parameter2 = 2 STR_VAR resource = ~u#hfsfws~ END // Caballus' Whispering Staff
    BUT_ONLY IF_EXISTS
    
  COPY_EXISTING ~u#hfdtdd.itm~ ~override~ // eefp curing deaf, feeblemind (remove magic)
    PATCH_IF FILE_EXISTS_IN_GAME ~#curedef.spl~ BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 81 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curedef~ END // swap cure deaf for spellcast  
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 112 END // delete removal of desf icon (handled by spell)
    END 
    PATCH_IF FILE_EXISTS_IN_GAME ~#curefbm.spl~ BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 77 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curefbm~ END // swap cure feeblemind for spellcast
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 48 END // delete removal of feeblemind icon (handled by spell)
    END  
    BUT_ONLY    

  COPY_EXISTING ~u#hfssls.itm~ ~override~ // Lolth's Cruel Sting - eefp curing hold, stun (free action)
    PATCH_IF FILE_EXISTS_IN_GAME ~#curehol.spl~ BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 162 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curehol~ END // swap cure hold for spellcast    
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 13 END // delete removal of hold icon (handled by spell)
    END  
    PATCH_IF FILE_EXISTS_IN_GAME ~#curestn.spl~ BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 46 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curestn~ END   // swap cure stun for spellcast    
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 55 END // delete removal of stun icon (handled by spell)
    END  
    BUT_ONLY
  
END
